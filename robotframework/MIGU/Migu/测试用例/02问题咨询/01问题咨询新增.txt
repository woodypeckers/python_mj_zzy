*** Settings ***
Suite Teardown    teardown_案例集结束时关闭浏览器
Resource          ../../Resource/Action/02问题咨询/01问题咨询新增.txt

*** Test Cases ***
01未登录问题咨询必填项检查
    [Documentation]    输入页面，不输入任何值，点击提交可以校验必填项
    [Tags]    delete
    打开partner端
    未登录点击问题咨询链接
    点击按钮    提交
    页面错误提示信息校验    业务类型    请选择业务类型
    页面错误提示信息校验    问题咨询名称    请输入问题咨询名称
    页面错误提示信息校验    详细描述    请输入详细描述
    页面错误提示信息校验    提交人所属公司    提交人所属公司
    页面错误提示信息校验    提交人邮箱    请输入提交人邮箱
    页面错误提示信息校验    提交人姓名    请输入提交人姓名
    页面错误提示信息校验    提交人手机号    请输入提交人手机号
    [Teardown]    teardown_案例运行失败时关闭浏览器

02未登录新增问题咨询
    [Tags]    delete
    打开partner端
    未登录点击问题咨询链接
    未登录填写问题咨询信息    业务咨询    咨询问题    问题咨询的详细描述，问题是注册不了账号    问题公司    wenti@wenti.com    15809090909
    ...    张问题
    点击按钮    提交
    校验二次确认框提示信息且点击按钮    您提交的业务咨询已经通过邮件发送到咪咕管理员，\n 我们将尽快处理，处理结果将以邮件方式回复！
    未登录问题咨询提交后数据校验    咨询问题${problem_id}    consult_req
    [Teardown]    teardown_案例运行失败时关闭浏览器

03已登录问题咨询必填项检查
    [Documentation]    输入页面，不输入任何值，点击提交可以校验必填项
    [Tags]    smoke
    插入正式的partner端账号    2
    登录partner端    ${account_name}
    partner端登录后进入正式页面
    已登录点击问题咨询链接
    点击按钮    提交
    页面错误提示信息校验    业务类型    请选择业务类型
    页面错误提示信息校验    问题咨询名称    请输入问题咨询名称
    页面错误提示信息校验    详细描述    请输入详细描述
    退出partner端且清除cookie
    [Teardown]    teardown_案例运行失败时关闭浏览器

04已登录新增问题咨询
    [Tags]    smoke
    插入正式的partner端账号    2
    登录partner端    ${account_name}
    partner端登录后进入正式页面
    已登录点击问题咨询链接
    已登录填写问题咨询信息    业务咨询    咨询问题    问题咨询的详细描述，问题是注册不了账号
    点击按钮    提交
    校验二次确认框提示信息且点击按钮    您提交的业务咨询已经通过邮件发送到咪咕管理员，\n 我们将尽快处理，处理结果将以邮件方式回复！
    已登录问题咨询提交后数据校验    咨询问题${problem_id}    consult_req
    partner端查询结果校验    咨询问题${problem_id}    业务咨询[咨询问题${problem_id}]    #第二个输入的参数为问题类型加上问题名称
    退出partner端且清除cookie
    [Teardown]    teardown_案例运行失败时关闭浏览器
